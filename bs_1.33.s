.feature labels_without_colons, pc_assignment
; ****************************
	FLAG =$0298
	FF =255
	SERI2 =$029B
	SERI1 =$029C
	SERO2 =$029E
	SERO1 =$029D
	SERST =$0297
	MAP  =$0299
	BLNKT =$0313
	BLNK1 =$02BE
	BLNK2 =$02BF
	ACIA =$D600
	MIKE  =1
.IF MIKE

	*=$ECD9
BC	.BYT 11,0

	*=$E535
	.BYT 15
	TPI =$D500
RTSB =4
.ELSE
	TPI =$DF00
	BC =$ECD9
.ENDIF

;
;********************************
;--------------------------------
;AENDERUNGEN AM ROM
;--------------------------------
;FARBE FUER CLR/HOME HINTERGRUND
	*=$E4DA
	LDA $0286

	*=$EB1C
	LDY #3; CURSOR SPEED

	*=$FF7D
	JMP NRESET; CLKHI BEI CONT UND RS232 INIT

;********************************
;--------------------------------
;        RS 232 PER ACIA
;--------------------------------
; BELEGT     $F014-$F0AC
;            $F409-$F49D
;--------------------------------
.IF MIKE
	SERTS =TPI+2
.ENDIF
;-------OPEN
	*=$F409
RSOPEN	LDY #0
	STY SERST
RS0	CPY $B7
	BEQ RS1
	LDA ($BB),Y
	STA $0293,Y
	INY
	CPY #$04
	BNE RS0
RS1	LDA $0293
	ORA #16
	SEI
	STA ACIA+1
	STA ACIA+3
	LDA $0294
	AND #$F0
	ORA #$07
	STA ACIA+2
	LDA ACIA+1
	LDA #0
	STA $02A1
	CLI
	JSR $FE27
	LDA $F8
	BNE RS2
	DEY
	STY $F8
	STX $F7
RS2	LDA $FA
	BNE RS3
	DEY
	STY $FA
	STX $F9
RS3	SEC
	LDA #$F0
	JMP $FE2D

;-------- RESET
NRESET	JSR FRESET
RSRESET	STA ACIA+1
	RTS

;-------- BASIN
RSBASIN	LDA SERST
	LDY SERI1
	CPY SERI2
	BEQ RSBIE
	AND #$F7
	STA SERST
	LDA ($F7),Y
	INC SERI1
	PHA
	LDA $0294
	AND #1
	BEQ RSBI1
	LDA SERI1
	SEC
	SBC SERI2
	CMP #$C0
	BCC RSBI1
.IF !MIKE
	LDA ACIA+2
	AND #$F3
	ORA #$04
	STA ACIA+2
.ELSE
	LDA SERTS
	AND #FF-RTSB
	STA SERTS
.ENDIF
RSBI1	PLA
	CLC
	RTS
RSBIE	ORA #$08
	STA SERST
	LDA #0
	RTS

;F49E
;-------- CLOSE
	*=$F2AF
	JSR RSRESET

	*=$F2C5
	JMP RS3

;-------- CHKIN
	*=$F227
	JMP RSCHKIN

;-------- CHKOUT
	*=$F26C
	JMP RSCKOUT

;-------- BSOUT
	*=$F014
	NOP
	NOP
	NOP
RSBSOUT	LDY SERO2
	INY
	CPY SERO1
	BEQ RSBSOUT
	STY SERO2
	DEY
	LDA $9E
	STA ($F9),Y
CRTS	CLC
	RTS

;-------- IRQ
RSIR1	TAX
	LDA SERST
	AND #$0C
	STA SERST
	TXA
	AND #$63
	ORA SERST
	STA SERST
;----
	TXA
	AND #%00001000
	BEQ SI3
	LDA ACIA
	LDY SERI2
	STA ($F7),Y
	INY
	LDA SERST
	CPY SERI1
	BNE SI2
	ORA #4
	STA SERST
SI2	STY SERI2
;----
	LDA $0294
	AND #1
	BEQ SI3
	LDA SERI1
	SEC
	SBC SERI2;=FREI
	CMP #$40
	BCS SI3; MEHR ALS 1/4
.IF !MIKE
	LDA ACIA+2
	AND #$F3
	STA ACIA+2
.ELSE
	LDA SERTS
	ORA #RTSB
	STA SERTS
.ENDIF
;----
SI3	TXA
	AND #%00010000
	BEQ SI6
	LDY SERO1
	CPY SERO2
	BEQ SI6
	LDA ($F9),Y
	JMP SII
;----
	JMP RSBASIN ;(	*=F086)
SII	STA ACIA
	INY
	STY SERO1
;----
SI6	LDA ACIA+1
	RTS
;----
RSIRQ	LDA ACIA+1
	BPL RSIR2
	JSR RSIR1
	JMP $EA81
RSIR2	JMP RSIR3
	NOP
	NOP
	RTS; F0A4
RSIR3	JSR $FFEA
	JMP BLIRQ

;$EA34
;-------- CHKIN/CKOUT
RSCHKIN	STA $99
	CLC
	RTS
RSCKOUT	STA $9A
	CLC
	RTS

;F0BD
;--------------------------------
;-------- NMIOUT ADRS
	*=$ED0E
	NOP
	NOP
	NOP

	*=$F88A
	NOP
	NOP
	NOP
;-------- NMI-EINSPRUNG LOESCHEN
	*=$FE54
	NOP
	NOP

	*=$FE72
	JMP $FEB6

;-------- IRQ SETZEN
	*=$EA31
	JMP RSIRQ

;********************************
;-------------------------------
	*=$E1E6
	JSR LSP; LOAD/SAVE IM DIR

	*=$E1D9
	LDX #8
	LDY #1
	JSR LSD

	*=$E5E7
	JSR KEY; F-TASTEN

	*=$ECE7 ; SHIFT RUN/STOP
	.BYT 'L','O'+$80,34,'*'

	*=$E44B
	.WORD NEUBEF ;BASIC-

	*=$E451
	.WORD GETAUS ;VEKTOREN

	*=$EC78
	.BYT $84 ;CTRL INST

	*=$ECAB
	.BYT $82 ;CTRL HOME

	*=$EC7A
	.BYT $81 ;CTRL CRSR RIGHT

;--------------------------------
	*=$EEBB
LSD	JSR $FFBA
TSTBA	LDA FLAG
	BPL LSP1
	AND #15
	STA $BA
	RTS
LSP	JSR $0079
	CMP #','
	BEQ LSP1
	PLA
	PLA
	JMP $A8F8
LSP1	RTS
KEY	JSR $E5B4
	BIT $9D
	BPL LSP1
	BIT FLAG
	BPL LSP1
	CMP #3
	BNE KEY2
CLR	LDX #0
	STX $C7
	STX $D4
	STX $D8
	BEQ LSP1
KEY2	CMP #$81
	BCC LSP1
	BNE KEY3
	LDY $D5
	INY
KL1	DEY
	BMI KL2
	LDA ($D1),Y
	CMP #' '
	BEQ KL1
KL2	STY $D3
	CPY $D5
	BEQ K3C
	INC $D3
	BNE K3C
KEY3	CMP #$82
	BNE KEY4
	LDX #24
	LDY #0
	CLC
	JSR $FFF0
K3C	JSR CLR
	JMP K3
KEY4	CMP #$84
	BNE KEY5
	LDA #' '
	LDY $D3
K4	CPY $D5
	BEQ K5
	BCS K3C
K5	STA ($D1),Y
	INY
	BNE K4
KEY5	CMP #140
	BEQ K8
	BCS LSP1
	CMP #137
	BEQ LSP1
	CMP #133
	BCC LSP1
	SBC #133
	TAY
	LDX KEY1,Y
	LDY #1
K1	LDA KT,X
	BEQ K2
KTX	STA $0276,Y
	INY
	INX
	BNE K1
K8	LDA #'@'
	STA $0277
	LDA FLAG
	AND #15
	EOR #1
	ORA #'0'
	STA $0278
	LDA #13
	STA $0279
	LDY #3
K2	STY $C6
K3	PLA
	PLA
	JMP $E5CD

KEY1	.BYT 0, KT3-KT, KT5-KT, KT7-KT
	.BYT <-1, KT4-KT, KT6-KT
KT	.BYT "LIST"
	.BYT 13,0
KT3	.BYT "RUN:"
	.BYT 13,0
KT5	.BYT "LOAD"
	.BYT 13,0
KT7	.BYT ">$0"
	.BYT 13,0
KT4	.BYT "_"
	.BYT 13,0
KT6	.BYT "SAVE"
	.BYT 0

GETAUS	BIT FLAG
	BMI GETOK
	JMP $AE86
GETOK	LDA #0
	STA $0D
	JSR $0073
	CMP #'$'
	BEQ GTA1
	CMP #'%'
	BEQ GTA2
	JSR $0079
	JMP $AE8D
GTA2	LDA #0
	STA $63
	STA $62
GTA2A	JSR $0073
	BCS GTAE
	CMP #'2'
	BCS GTAERR
	LSR
	ROL $63
	ROL $62
	BCC GTA2A
GTAERR	JMP $B248 ; ILL. QUANTITY
GTA1	LDA #0
	STA $62
GTA1A	STA $63
	JSR $0073
	SEC
	SBC #'0'
	BCC GTAE
	CMP #10
	BCC GTA1O
	SBC #'A'-'9'-1
	CMP #10
	BCC GTAE
	CMP #16
	BCS GTAERR
GTA1O	PHA
	LDY #4
GTA3	ASL $63
	ROL $62
	BCS GTAERR
	DEY
	BNE GTA3
	PLA
	ORA $63
	JMP GTA1A
GTAE	LDX #$90
	SEC
	JSR $BC49
	JMP $0079
;F014
;--------------------------------
;--------------------------------
	*=$FE75
NB1F	LDA #0
	STA $90
	LDA $BA
	JSR $FFB1
	LDA $0201
	CMP #'$'
	BNE NB1C
	LDA #$F0
	.BYT $2C
NB1C	LDA #$FF
	JSR $FF93
	LDA $90
	BMI NBA1E
	LDY #0
NBA1A	LDA $0201,Y
	BEQ NB1E
	JSR $FFA8
	INY
	BNE NBA1A
NB1E	JSR $FFAE
	LDA $90
	BNE NBA1E
	LDA $BA
	JSR $FFB4
	LDA $0201
	CMP #'$'
	JMP NB1G
;FFB6
;--------------------------------
	*=$FEC2
NB1G	BEQ NB1D
	LDA #$FF
	JSR $FF96
NB1B	JSR $FFA5
	JSR $E716
	LDA $90
	BEQ NB1B
NBDE	JSR $FFAB
NBA1E	PLA
	PLA
	JMP $E386
NB1D	LDA #$F0
	JSR $FF96
	JSR $FFA5
	JSR $FFA5
NBDL	JSR $FFA5
	JSR $FFA5
	LDX $90
	BNE EDL3
	JSR $FFA5
	TAX
	JSR $FFA5
	JSR $BDCD
EDL1	JSR $FFA5
	LDX $90
	BNE EDL2
	JSR $E716
	CMP #0
	BNE EDL1
EDL2	LDA #13
	JSR $E716
	JSR $FFE4
	BNE EDL3
	LDA $90
	BEQ NBDL
EDL3	JSR $FFAB
	LDA $BA
	JSR $FFB1
	LDA #$E0
	JSR $FF93
	JSR $FFAE
	JMP NBA1E
	STA ACIA+1
	JMP $FF6E
;FF48
;******** KASSETTE LOESCHEN *****
	*=$F5F8
	BCC $F5F1 ; SAVE

	*=$F4B6
	BCC $F4AF ; LOAD

	*=$F38B
	JMP $F713 ; OPEN

	*=$F2C8
	JMP $F713 ; CLOSE

	*=$F26F
	JMP $F713 ; CKOUT

	*=$F225
	BNE $F26F ; CHKIN

	*=$F1E5
	SEC
	BCS $F1FD ; BSOUT

	*=$F179
	SEC
	RTS   ; BASIN

;******* PARALLELER IEC-BUS *****
Z1 =$94
Z2 =$95
Z4 =$A3
Z3 =$0285
B1 =$FE1C
B2 =$DC07
B3 =$DC0D
B4 =$DC0F
B5 =TPI
B6 =TPI+1
B7 =TPI+3
B8 =TPI+4
C1 =B5
C2 =TPI+2
C3 =TPI+3
C4 =B8
C5 =TPI+5
	*=$FDAB
	JSR NINI

	*=$FD50
	JMP NMINI

	*=$ED0E
	JMP NTALK; UND LISTEN

	*=$EDBB
	JMP NSECL

	*=$EDC9
	JMP NSECT

	*=$EDDD
	JMP NOUT
	NOP

	*=$EDF0
	JMP NUT

	*=$EE00
	JMP NUL

	*=$EE13
	JMP NIN

	*=$F281
	JSR NATNHI

	*=$F72C
NINI	STA $DC00
IECINI
.IF MIKE
	LDA #%00011100
	STA C5
	LDA #0
	STA C2
.ENDIF
	LDA C2
	AND #239
	STA C2
	LDY #0
	STY C4
	LDA #58
	STA C1
	LDA #63
	STA C3
	LDA C2
	AND #254
	STA C2
	LDY #255
L0	DEY
	NOP
	BNE L0
	LDA C2
	ORA #1
	STA C2
	LDA #57
	STA C1
	JMP $FDAE
	;RTS
;*****
IECTALK	ORA #64
	.BYT $2C
IECLISTEN
	ORA #32
IECTL
L1	PHA
	LDA #59
	STA B7
	LDA #255
	STA B6
	STA B8
	LDA #250
	STA B5
	LDA Z1
	BPL L2
	LDA B5
	AND #223
	STA B5
	LDA Z2
	JSR L7
	LDA Z1
	AND #127
	STA Z1
	LDA B5
	ORA #32
	STA B5
L2	LDA B5
	AND #247
	STA B5
	PLA
	JMP L7
SECLISTEN
	JSR L7
L3	LDA B5
	ORA #8
	STA B5
	RTS
SECTALK	JSR L7
L4	LDA #61
	AND B5
	STA B5
	LDA #195
	STA B7
	LDA #0
	STA B8
	BEQ L3
IECOUT	PHA
	LDA Z1
	BPL L5
	LDA Z2
	JSR L7
	LDA Z1
L5	ORA #128
	STA Z1
	PLA
	STA Z2
	CLC
	RTS
UNTALK	LDA #95
	BNE L6
UNLISTEN
	LDA #63
L6	JSR L1
	JSR L4
	LDA #253
	ORA B5
	STA B5
	CLI
	RTS
L7	EOR #255
	STA B6
	LDA B5
	ORA #18
	STA B5
	BIT B5
	BVC L8
	BPL L8
	LDA #128
	JSR B1
	BNE L12
L8	LDA B5
	BPL L8
	AND #239
	STA B5
L9	JSR L18
L10	BIT B5
	BVS L11
	LDA B3
	AND #2
	BEQ L10
	LDA Z3
	BMI L9
	LDA #1
	JSR B1
L11	LDA B5
	ORA #16
	STA B5
L12	LDA #255
	STA B6
	RTS
IECIN	LDA B5
	AND #189
	ORA #129
	STA B5
L13	JSR L18
L14	LDA B5
	AND #16
	BEQ L15
	LDA B3
	AND #2
	BEQ L14
	LDA Z3
	BMI L13
	LDA #2
	JSR B1
	LDA B5
	AND #61
	STA B5
	LDA #13
	CLC
	RTS
L15	LDA B5
	AND #127
	STA B5
	AND #32
	BNE L16
	LDA #64
	JSR B1
L16	LDA B6
	EOR #255
	PHA
	LDA B5
	ORA #64
	STA B5
L17	LDA B5
	AND #16
	BEQ L17
	LDA B5
	AND #191
	STA B5
	PLA
	CLC
	RTS
L18	LDA #255
	STA B2
	LDA #17
	STA B4
	LDA B3
	RTS
;**** EINBINDUNG
NTALK	JSR SETDEV
	BIT MAP
	BPL OTL
	JMP IECTL
OTL	JSR $F0A4
	JMP $ED11
NSECL	BIT MAP
	BPL OSL
	JMP SECLISTEN
OSL	JSR $ED36
	JMP $EDBE
NSECT	BIT MAP
	BPL OST
	JMP SECTALK
OST	JSR $ED36
	JMP $EDCC
NOUT	BIT MAP
	BPL OOUT
	JMP IECOUT
OOUT	BIT $94
	BMI OOUT1
	JMP $EDE1
OOUT1	JMP $EDE6
NUT	BIT MAP
	BPL OUT
	JMP UNTALK
OUT	JSR $EE8E
	JMP $EDF3
NUL	BIT MAP
	BPL OUL
	JMP UNLISTEN
OUL	JSR $ED11
	JMP $EE03
NIN	BIT MAP
	BPL OIN
	JMP IECIN
OIN	SEI
	LDA #0
	JMP $EE16
SETDEV	PHA
	STY Z4
	AND #15
	SEC
	SBC #4
	BCC PIEC
	CMP #7
	BCS SIEC1;PIEC
	TAY
	LDA POT2,Y
	LDY Z4
	LSR Z4
	AND MAP
	BEQ PIEC
SIEC1	LDA MAP
	AND #127
SIEC	STA MAP
	PLA
	RTS
PIEC	LDA MAP
	ORA #128
	BNE SIEC

POT2	.BYT 1,2,4,8,$10,$20,$40

NMINI	LDA #0
	TAY
	JSR $FD53
.IF MIKE
	LDA #%10000001
.ELSE
	LDA #%10000000
.ENDIF
	STA MAP
	LDA #128
	STA 650
FRESET	LDA #8+128
	STA FLAG
	LDA #180
	STA BLNK2
	STA BLNKT
.IF MIKE
	LDA #50
.ELSE
	LDA #0
.ENDIF
	STA BLNK1
	RTS

NATNHI	BIT MAP
	BPL OAH
	JMP L3
OAH	JMP $EDBE
;***** WAS NICHT MIT KASSETE GEHT
NEUBEF	BIT FLAG
	BPL NEUEND
	JSR TSTBA
	LDA $0200
	CMP #'@'
	BEQ NBA1
	CMP #'>'
	BEQ NBA1
	CMP #'!'
	BEQ NBA2
	CMP #'_'
	BEQ SYSOFF
NEUEND	JMP $A57C
NBA2	LDA #1
	TAY
	STA ($2B),Y
	JSR $A533
	LDA $22
	CLC
	ADC #2
	STA $2D
	LDA $23
	ADC #0
	STA $2E
	JSR $A663
	JMP $E386
NBA1	JSR $0073
	BCC NBZ1
	JMP NB1F
NBZ1	JSR $B79E
	TXA
	AND #15
	ORA #128
	.BYT $2C
SYSOFF	LDA #0
NBA1EX	STA FLAG
	JMP NBA1E

;*******************************
;***** SCROLL-HALT
	PCX1=*
	*=$E940
	EOR #%00100100
	AND #%00100100
	BEQ $E956
	JMP PCX1
	NOP
	NOP

	*=PCX1
	CMP #%00000100
	BEQ S1;CTRL LANGSAM
	CMP #%00100000
	BNE S2;CTRL/CBM STOP
	;JSR TESTHCOPY;NUR CBM
S0	JMP $E938
S1	JMP $E94B
S2	LDA $DC01
	CMP #$FF
	BNE S2
S3	LDA $DC01
	AND #$20
	BNE S3
	BEQ S0

;********************************
.IF 0
	TESTHCOPY LDA #$7F
	STA $DC00
	HC1 LDA $DC01
	CMP $DC01
	BNE HC1
	AND #%00100010
	BEQ HCOPY
	CLC
	RTS
.ENDIF
;********************************

	LB0 =$B0
	LB1 =LB0+1
	L028E =$028E
	HCDEV =4
	HCSEC =7
HCOPY	;INC 53280
	LDA #$20
	BIT $D011; TEST HIRES
	BNE LC1AA
	LDA L028E
	BMI LC1AA
	LDA #$80
	STA L028E
	LDA $DD00; ADRESSE BERECHNEN
	LSR
	LDA $D018
	AND #$70
	ROR
	TAX
	LDA $DD00
	LSR
	LSR
	TXA
	ROR
	EOR #$C0
	STA LB1; HIBYTE
	LDA #$00
	STA LB0
	LDA #HCDEV
	JSR $ED0C; LISTEN
	LDA #$60+HCSEC; SECLISTEN
	JSR $EDB9
	LDA #13
	JSR $EDDD
	LDX #25
LC1E0	LDY #$00
LC1E2	LDA (LB0),Y
	AND #$7F
	CMP #$40
	BCC LC1F2
	CMP #$60
	BCC LC1F0
	EOR #$40
LC1F0	EOR #$80
LC1F2	CMP #$20
	BCS LC1F8
	EOR #$40
LC1F8	JSR $EDDD
	INY
	CPY #$28
	BCC LC1E2
	LDA #$0D
	JSR $EDDD
	TYA
	CLC
	ADC LB0
	STA LB0
	BCC LC22A
	INC LB1
LC22A	DEX
	BNE LC1E0
	JSR $EDFE; UNLISTEN
	JSR $EDFE
	LDA #$00
	STA L028E
LC1AA	SEC
	RTS

;*******************************
	PCX2=*
	*=$EB76
	JMP HC2

	*=PCX2
HC2	CPX #4
	BNE HCE
	LDA $CB
	CMP #$39
	BNE HCE
	BIT $C6
	BPL HC
HCE	JMP $EAE0
HC	LDA #<-1
	STA $C6
	JSR HCOPY
	LDA #0
	STA $C6
	JMP $EB42
;*******************************

BLIRQ	LDX BLNK1
	BEQ BLXX
	LDX $D011
	TXA
	AND #%11101111
	LDY BLNK2
	CPY #255
	BEQ BLANK
	TXA
	AND #%00010000
	BNE BLXX
	TXA
	LDX BC
	ORA #%00010000
	.BYT $2C
BLANK	LDX #0
	STA $D011
	STX 53280
BLXX	JMP $EA34

;*******************************
	PCX3=*
	*=$EA98
	JMP BLNK

	*=PCX3
BLNK	BEQ NOTPR
	TAY
	LDA BLNKT
	STA BLNK2
	JMP $EA9B
NOTPR	LDA BLNK1
	BEQ BLE
	DEC BLNK1
	BNE BLE
	LDA #50
	STA BLNK1
	LDA BLNK2
	CMP #255
	BEQ BLE
	DEC BLNK2
BLE	JMP $EB26

	PCX4=*
	*=$E716
	JMP BLS

	*=PCX4
BLS	PHA
	STA $D7
	LDA BLNKT
	STA BLNK2
	JMP $E719
;******** LEERE BEREICHE LOESCHEN
;FCD1
;*******************************
